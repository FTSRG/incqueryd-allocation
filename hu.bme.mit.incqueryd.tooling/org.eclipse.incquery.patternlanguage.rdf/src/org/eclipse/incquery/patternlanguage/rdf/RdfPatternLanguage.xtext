grammar org.eclipse.incquery.patternlanguage.rdf.RdfPatternLanguage with org.eclipse.incquery.patternlanguage.PatternLanguage

import "http://www.eclipse.org/incquery/patternlanguage/PatternLanguage" as core
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate rdfPatternLanguage "http://www.eclipse.org/incquery/patternlanguage/rdf/RdfPatternLanguage"

RdfPatternModel returns core::PatternModel:
	{RdfPatternModel}
	vocabularies+=Vocabulary*
	('base' baseIriValue=RawIri)?
	iriPrefixes+=IriPrefix*
	patterns+=Pattern*
;

Vocabulary:
	'vocabulary' location=RawIri
;

IriPrefix:
	'prefix' name=ID ':' value=RawIri
;

Iri:
	(prefix=[IriPrefix] ':')? value=RawIri
;

Parameter returns Variable:
	name=ID
;

Constraint returns core::Constraint:
	PatternCompositionConstraint |
	CompareConstraint |
	RdfClassConstraint |
	RdfPropertyConstraint |
	RdfCheckConstraint
;

RdfClassConstraint:
	type=Iri '(' variable=VariableReference ')'
;

RdfPropertyConstraint:
	refType=Iri '(' source=VariableReference ',' target=ValueReference ')'
;

RdfCheckConstraint:
	'check' '(' expression=STRING ')'
;

ValueReference returns core::ValueReference:
	LiteralValueReference |
	VariableValue |
	AggregatedValue
;

LiteralValueReference returns core::LiteralValueReference:
	IntValue |
	DoubleValue |
	StringValue |
	BoolValue |
	RdfLiteral
;

RdfLiteral:
	lexicalForm=STRING (('^^' datatype=Iri) | ('@' language=ID))
;

RawIri returns ecore::EString:
	ID | ESCAPED_IRI
;

terminal ESCAPED_IRI:
	'<' ('\\>' | !'>')* '>'
;